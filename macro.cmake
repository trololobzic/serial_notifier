#wrapper for directories_structure macro
macro(create_directories_structure curdir root_folder_name)
  if(NOT IS_DIRECTORY ${PROJECT_SOURCE_DIR}/${curdir})
    message(FATAL_ERROR "${curdir} is not a directory")
  endif()
  
  message(STATUS "generate directories structure: ${curdir}")
  directories_structure(${curdir} ${curdir} ${root_folder_name})
endmacro()

macro(directories_structure curdir old_root_name new_root_name)
  file(GLOB children RELATIVE ${PROJECT_SOURCE_DIR}/${curdir} ${PROJECT_SOURCE_DIR}/${curdir}/*)
  foreach(child ${children})
    if(IS_DIRECTORY ${PROJECT_SOURCE_DIR}/${curdir}/${child})
	  message(STATUS "generate directories structure: ${curdir}/${child}")
      directories_structure(${curdir}/${child} ${old_root_name} ${new_root_name})
    else()
      string(REPLACE "/" "\\" groupname ${curdir})
	  string(REPLACE ${old_root_name} ${new_root_name} groupname ${groupname})
      source_group(${groupname} FILES ${PROJECT_SOURCE_DIR}/${curdir}/${child})
    endif()
  endforeach()
endmacro()

macro(force_set_flags var these those)
  if("${${var}}" MATCHES "${these}")
    string(REGEX REPLACE "${these}" "${those}" ${var} "${${var}}")
  else()
    set(${var} "${${var}} ${those}")
  endif()
endmacro()

macro(replace_flags var these those)
  if("${${var}}" MATCHES "${these}")
    string(REGEX REPLACE "${these}" "${those}" ${var} "${${var}}")
  endif()
  message(STATUS "info: ${var}='${${var}}'")
endmacro()

macro(msvc_link_to_static_crt)
  if(MSVC)
    set(has_correct_flag 0)
    foreach(lang C CXX)
    foreach(suffix "" _DEBUG _MINSIZEREL _RELEASE _RELWITHDEBINFO)
      replace_flags("CMAKE_${lang}_FLAGS${suffix}" "/MD" "/MT")
      if(CMAKE_${lang}_FLAGS${suffix} MATCHES "/MT")
        set(has_correct_flag 1)
      endif()
    endforeach()
    endforeach()
    if(NOT has_correct_flag)
      message(FATAL_ERROR "no CMAKE_*_FLAGS var contains /MT")
    endif()
  endif()
endmacro()